#
# This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0. If a copy of the MPL was not
# distributed with this file, You can obtain one at http://mozilla.org/MPL/2.0/.
#

import os

SConscript('../../Scripts/SCons/Shared.sconscript.py')
Import('*')

env = baseEnv.Clone()
env['CCFLAGS'] += baseEnv['SHCCFLAGS'][1:]
env['CPPPATH'] += ['#/Source/include', '#/Source/code/BoostWorkaround', '#/Source/contrib/openddlparser/include',
                   GetDependencyCPPPATH('ZLib')]
env['CPPDEFINES'] += ['ASSIMP_BUILD_NO_BLEND_IMPORTER', 'ASSIMP_BUILD_NO_COB_IMPORTER', 'ASSIMP_BUILD_NO_C4D_IMPORTER',
                      'ASSIMP_BUILD_NO_COLLADA_IMPORTER', 'ASSIMP_BUILD_NO_DAE_IMPORTER', 'ASSIMP_BUILD_NO_DXF_IMPORTER',
                      'ASSIMP_BUILD_NO_FBX_IMPORTER', 'ASSIMP_BUILD_NO_IFC_IMPORTER', 'ASSIMP_BUILD_NO_OWN_ZLIB']

target = 'OpenAssetImport'
if compiler.startswith('VisualStudio'):
    target += compiler + {True: 'Debug', False: ''}[isDebugBuild]

# Workaround for -O3 causing Clang to crash when building iOS release builds (problem started with Xcode 7.3)
if platform == 'iOS' and isReleaseBuild:
    env['CCFLAGS'].remove('-O3')
    env['CCFLAGS'] += ['-O1']

result = SConscript('Source/SConscript', variant_dir=baseVariantDir, exports='env target', duplicate=0)

Install(os.path.join('Library', platform, architecture), result)
